use std::str::FromStr;

use crate::Dice;

grammar;

pub Expr: (i32, String) = {
    <l:Expr> "+" <r:Factor> => (l.0 + r.0, format!("{} + {}", l.1, r.1)),
    <l:Expr> "-" <r:Factor> => (l.0 - r.0, format!("{} - {}", l.1, r.1)),
    Factor,
};

Factor: (i32, String) = {
    <l:Factor> "*" <r:Term> => (l.0 * r.0, format!("{} * {}", l.1, r.1)),
    <l:Factor> "/" <r:Term> => (l.0 / r.0, format!("{} / {}", l.1, r.1)),
    <l:Factor> "//" <r:Term> => (l.0 / r.0, format!("{} // {}", l.1, r.1)),
    <l:Factor> "^" <r:Term> => (l.0.pow(r.0 as u32), format!("{} ^ {}", l.1, r.1)),
    <l:Factor> "**" <r:Term> => (l.0.pow(r.0 as u32), format!("{} ** {}", l.1, r.1)),
    <l:Factor> "%" <r:Term> => (l.0 % r.0, format!("{} % {}", l.1, r.1)),
    Term,
};

Term: (i32, String) = {
    Dice,
    Num,
    "(" <Expr> ")",
};

Dice: (i32, String) = {
    <l:Num> "d" <r:Num> => {
        let dice = Dice::new(l.0 as usize, r.0).roll();
        (dice.result, dice.roll_str())
    },
    "d" <r:Num> => {
        let dice = Dice::new(1, r.0).roll();
        (dice.result, dice.roll_str())
    },
}

Num: (i32, String) = {
    r"[0-9]+" => (i32::from_str(<>).unwrap(), <>.to_string()),
};
